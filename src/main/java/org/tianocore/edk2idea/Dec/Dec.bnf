{
  parserClass="org.tianocore.edk2idea.Dec.DecParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Dec"
  psiImplClassSuffix="Impl"
  psiPackage="org.tianocore.edk2idea.Dec.psi"
  psiImplPackage="org.tianocore.edk2idea.Dec.psi.impl"

  elementTypeHolderClass="org.tianocore.edk2idea.Dec.psi.DecTypes"
  elementTypeClass="org.tianocore.edk2idea.Dec.psi.DecElementType"
  tokenTypeClass="org.tianocore.edk2idea.Dec.psi.DecTokenType"

  tokens = [
      LBRACKET="["
      RBRACKET="]"
      LPAREN="("
      RPAREN=")"
      LBRACE="{"
      RBRACE="}"
      DOT="."
      COMMA=","
      EQ="="
      PIPE="|"
      COMMENT="regexp:#[^\n]*"
      CRLF="regexp:\n|\r|\r\n"
      WHITE_SPACE="regexp:[ \t\f]+"
      
      HEX_VAL="regexp:0[xX][0-9a-fA-F]+"
      INT_VAL="regexp:[0-9]+"
      BOOL_VAL="regexp:(TRUE|FALSE|true|false)"
      
      SECTION_NAME="regexp:[a-zA-Z0-9_.]+"
      PCD_SECTION_HEADER="regexp:\[Pcds[^\]]*\]"
      PCD_NAME_TOKEN="regexp:[a-zA-Z0-9_]+(\.[a-zA-Z0-9_]+)+"
      WORD="regexp:[^\[\]=\s|{},]+"
      STRING="regexp:\"[^\"]*\""
      PATH_STRING="regexp:[a-zA-Z0-9_\-\.\/\\\$]+"
      
      SLASH="/"
      BACKSLASH="\"
      LT="<"
      GT=">"
      MACRO_REF="regexp:\$\([a-zA-Z0-9_\.]+\)"
  ]
}

decFile ::= (decSection | property | emptyLineOrComment)*

decSection ::= (
    definesSection |
    includesSection |
    libraryClassesSection |
    guidsSection |
    protocolsSection |
    ppisSection |
    pcdsSection |
    userExtensionsSection |
    genericSection
)

property ::= key EQ value {methods=[getKey getValue]}

private emptyLineOrComment ::= CRLF | COMMENT | WHITE_SPACE

// [Defines]
definesSection ::= LBRACKET "Defines" RBRACKET CRLF* (property | emptyLineOrComment)*

// [Includes]
includesHeader ::= "Includes" sectionModifiers?
includesSection ::= LBRACKET includesHeader (COMMA includesHeader)* RBRACKET CRLF* (includePath | emptyLineOrComment)*
includePath ::= path {methods=[getPath]}

// [LibraryClasses]
libraryClassesHeader ::= "LibraryClasses" sectionModifiers?
libraryClassesSection ::= LBRACKET libraryClassesHeader (COMMA libraryClassesHeader)* RBRACKET CRLF* (libraryClassEntry | emptyLineOrComment)*
libraryClassEntry ::= libraryClassName PIPE libraryPath {methods=[getName getPath]}

// [Guids], [Protocols], [Ppis]
guidsHeader ::= "Guids" sectionModifiers?
guidsSection ::= LBRACKET guidsHeader (COMMA guidsHeader)* RBRACKET CRLF* (guidEntry | emptyLineOrComment)*

protocolsHeader ::= "Protocols" sectionModifiers?
protocolsSection ::= LBRACKET protocolsHeader (COMMA protocolsHeader)* RBRACKET CRLF* (guidEntry | emptyLineOrComment)*

ppisHeader ::= "Ppis" sectionModifiers?
ppisSection ::= LBRACKET ppisHeader (COMMA ppisHeader)* RBRACKET CRLF* (guidEntry | emptyLineOrComment)*

guidEntry ::= guidName EQ guidValue {methods=[getName getValue]}

// [Pcds...]
pcdsSection ::= PCD_SECTION_HEADER CRLF* (pcdEntry | emptyLineOrComment)*
pcdEntry ::= pcdName PIPE pcdValue PIPE pcdType PIPE pcdToken pcdConfiguration? {methods=[getName getValue getType getToken]}

// [UserExtensions]
userExtensionsHeader ::= "UserExtensions" sectionModifiers?
userExtensionsSection ::= LBRACKET userExtensionsHeader (COMMA userExtensionsHeader)* RBRACKET CRLF* (userExtensionContent | emptyLineOrComment)*
userExtensionContent ::= (WORD | PCD_NAME_TOKEN | STRING | DOT | SLASH | BACKSLASH | MINUS | EQ | PIPE | COMMA | LBRACE | RBRACE | HEX_VAL | INT_VAL | BOOL_VAL | LPAREN | RPAREN | MACRO_REF)+

// Generic fallback
genericHeader ::= WORD sectionModifiers?
genericSection ::= LBRACKET genericHeader (COMMA genericHeader)* RBRACKET CRLF* (genericEntry | emptyLineOrComment)*
genericEntry ::= (WORD | PCD_NAME_TOKEN | EQ | PIPE | COMMA | LBRACE | RBRACE | HEX_VAL | INT_VAL | BOOL_VAL | MACRO_REF)+

// Common elements
sectionModifiers ::= (DOT (WORD | STRING | MACRO_REF))+ (COMMA (DOT (WORD | STRING | MACRO_REF))+)*

key ::= WORD
value ::= (WORD | STRING | HEX_VAL | INT_VAL | BOOL_VAL | DOT | MINUS | SLASH | BACKSLASH | MACRO_REF)+

libraryClassName ::= WORD
libraryPath ::= path

guidName ::= WORD
guidValue ::= LBRACE guidStructure RBRACE
guidStructure ::= ((HEX_VAL | INT_VAL) COMMA?)+ (LBRACE ((HEX_VAL | INT_VAL) COMMA?)+ RBRACE)?

pcdName ::= PCD_NAME_TOKEN {
    mixin="org.tianocore.edk2idea.Dec.psi.impl.DecPcdNameMixin"
    implements="com.intellij.psi.PsiNameIdentifierOwner"
}
pcdValue ::= (WORD? STRING) | pcdName | HEX_VAL | INT_VAL | BOOL_VAL | guidValue
pcdType ::= "BOOLEAN" | "UINT8" | "UINT16" | "UINT32" | "UINT64" | "VOID*" | WORD
pcdToken ::= HEX_VAL | INT_VAL

pcdConfiguration ::= LBRACE (LT | GT | WORD | PCD_NAME_TOKEN | SLASH | DOT | MINUS | HEX_VAL | INT_VAL | LPAREN | RPAREN | WHITE_SPACE | CRLF)* RBRACE

path ::= (WORD | SLASH | DOT | BACKSLASH | MINUS) + {
  mixin="org.tianocore.edk2idea.Dec.psi.impl.DecPathMixin"
}

